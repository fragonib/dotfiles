#!/usr/bin/env zsh

CMD_OK="✓"
CMD_KO="✗"
GIT_NONE="✓"
GIT_DIRTY="✗"
START="➤"

prompt_jarvis_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_jarvis_status() {
  print -n "%(?:%F{green}[%F{white}$CMD_OK%F{green}]:%F{red}[%F{white}$CMD_KO%F{red}])"
}

prompt_jarvis_pwd() {
  local -r prompt_short_dir=$("$DOTLY_PATH/bin/dot" core short_pwd)
  print -n "%F{yellow}${prompt_short_dir}"
}

prompt_jarvis_git() {
  [[ -n ${git_info} ]] && print -n "%F{white}(${(e)git_info[prompt]}%F{white})"
}

prompt_jarvis_start() {
  print -n "%F{white}$START "
}

prompt_jarvis_setup() {

  autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_jarvis_precmd

  prompt_opts=(cr percent sp subst)

  zstyle ':zim:git-info:branch' format '%F{green}%b'
  zstyle ':zim:git-info:commit' format '%c'
  zstyle ':zim:git-info:clean' format '%F{white}$GIT_NONE'
  zstyle ':zim:git-info:dirty' format '%F{yellow}$GIT_DIRTY'
  zstyle ':zim:git-info:keys' format 'prompt' '%F{cyan}%b%c %C%D'

  PS1="\$(prompt_jarvis_status) \$(prompt_jarvis_pwd) \$(prompt_jarvis_git) \$(prompt_jarvis_start)"
  RPS1=''
}

prompt_jarvis_setup "${@}"
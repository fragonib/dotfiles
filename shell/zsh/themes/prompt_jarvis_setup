#!/usr/bin/env zsh

NONE="✓"
DIRTY="×"

prompt_jarvis_pwd() {
  local -r prompt_short_dir=$("$DOTLY_PATH/bin/dot" core short_pwd)
  print -n "%F{yellow}${prompt_short_dir}"
}

prompt_jarvis_git() {
  [[ -n ${git_info} ]] && print -n "%F{white} on${(e)git_info[prompt]}"
}

prompt_jarvis_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_jarvis_setup() {
  local prompt_jarvis_status='%(?:%F{green}[%F{white}$NONE%F{green}]:%F{red}[%F{white}$DIRTY%F{red}])'

  autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_jarvis_precmd

  prompt_opts=(cr percent sp subst)

  zstyle ':zim:git-info:branch' format '%F{green}%b'
  zstyle ':zim:git-info:commit' format '%c'
  zstyle ':zim:git-info:clean' format '%F{white}$NONE'
  zstyle ':zim:git-info:dirty' format '%F{yellow}$DIRTY'
  zstyle ':zim:git-info:keys' format 'prompt' ' %F{cyan}%b%c %C%D'

  PS1="${prompt_jarvis_status} \$(prompt_jarvis_pwd)\$(prompt_jarvis_git)%f "
  RPS1=''
}

prompt_jarvis_setup "${@}"
